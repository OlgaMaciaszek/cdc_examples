buildscript {
	repositories {
		mavenCentral()
		mavenLocal()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE")
		classpath 'io.codearte.accurest:accurest-gradle-plugin:0.7.1'
	}
}

ext {
	spockVersion = '1.0-groovy-2.4'
	restAssuredVersion = '2.4.1'

	accurestStubsBaseDirectory = 'src/test/resources/stubs'
}

subprojects {
	apply plugin: 'groovy'
	apply plugin: 'maven-publish'


	repositories {
		mavenCentral()
		mavenLocal()
		jcenter()
	}

	dependencies {
		testCompile "org.codehaus.groovy:groovy-all:2.4.3"
		testCompile "org.spockframework:spock-core:$spockVersion"
		testCompile("junit:junit:4.12")
		testCompile('com.github.tomakehurst:wiremock:1.52') {
			exclude group: 'org.mortbay.jetty', module: 'servlet-api'
		}
	}

		task allDeps(type: DependencyReportTask) {}

}

configure(project(':fraudDetectionService')) {
	apply plugin: 'spring-boot'
	apply plugin: 'accurest'

	ext {
		wireMockStubsOutputDirRoot = file("${project.buildDir}/production/${project.name}-stubs/")
		wireMockStubsOutputDir = new File(wireMockStubsOutputDirRoot, 'mappings/com/blogspot/toomuchcoding/')
	}

	accurest {
		targetFramework = 'Spock'
		testMode = 'MockMvc'
		baseClassForTests = 'com.blogspot.toomuchcoding.MvcSpec'
		contractsDslDir = file("${project.projectDir.absolutePath}/mappings/")
		generatedTestSourcesDir = file("${project.buildDir}/generated-sources/")
		stubsOutputDir = wireMockStubsOutputDir
	}

	jar {
		version = '0.0.1'
	}

	dependencies {
		compile("org.springframework.boot:spring-boot-starter-web") {
			exclude module: "spring-boot-starter-tomcat"
		}
		compile("org.springframework.boot:spring-boot-starter-jetty")
		compile("org.springframework.boot:spring-boot-starter-actuator")
		compile 'com.ofg:micro-infra-spring-boot-starter:0.9.1'

		testRuntime "org.spockframework:spock-spring:$spockVersion"
		testCompile "org.springframework:spring-test"
		testCompile "com.jayway.restassured:rest-assured:$restAssuredVersion"
		testCompile "com.jayway.restassured:spring-mock-mvc:$restAssuredVersion"
	}

}

configure(project(':fraudDetectionService')) {
	test.dependsOn('generateWireMockClientStubs')

	task stubsJar(type: Jar, dependsOn: 'generateWireMockClientStubs') {
		baseName = "${project.name}-stubs"
		from wireMockStubsOutputDirRoot
	}

	artifacts {
		archives stubsJar
	}

	publishing {
//		repositories {
//			maven {
//				url getProperty('mavenRepoUrl')
//				credentials {
//					username getProperty('mavenUser')
//					password getProperty('mavenPassword')
//				}
//			}
		publications {
			stubs(MavenPublication) {
				groupId "com.blogspot.toomuchcoding"
				artifactId "${project.name}-stubs"
				version '0.0.1'

				artifact stubsJar

			}
		}
	}
}
